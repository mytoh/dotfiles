#!/bin/tcsh

# this script depends on system FreeBSD, gawk, bwm-ng, and some xbm icon files

set date_format = '%a %d/%m %H:%M'
set icons = $HOME/.dzen/icons
set interval = 1

while (1)
  set date = `date +"$date_format"`

  set cpu = `top -b -d 2 | grep ^CPU | awk '{print $2,$6}'`

  set rx = `bwm-ng -o plain -c 1 -I em0 -T avg| grep em0 | awk '{print $2,$3}'`

  set tx = `bwm-ng -o plain -c 1 -I em0 -T avg| grep em0 | awk '{print $4,$5}'`

  set freemem = `vmstat -h |grep -n 2| awk '{print $6}'`

  set battery = `sysctl -n hw.acpi.battery.life`

  if ( $battery <= 100 && $battery > 50 ) then 
   set baticon = "$icons/sm4tik/bat_full_01.xbm";
  else if ( $battery >= 10 && $battery <= 50 ) then 
   set baticon = "$icons/sm4tik/bat_low_01.xbm";
  else 
   set baticon = "$icons/sm4tik/bat_empty_01.xbm";
  endif

  set batstate = `sysctl -n hw.acpi.battery.state`
  set batcolour = "#ffffff"
  if ( $batstate == 1 ) then
      set batcolour = "#ff4500";
  else if ( $batstate == 2 ) then
      set batcolour = "#00ff00";
  endif

  set diskused = `df -ch | grep total | awk '{print $3}' | cut -c 1-3`
  set disktotal = `df -ch | grep total | awk '{print $2}'`

  set iconcolour = "#ffffff"

  echo "  ^fg($iconcolour)^i($icons/sm4tik/cpu.xbm)^fg(#73f0ff) $cpu ^fg($iconcolour)^i($icons/sm4tik/mem.xbm)^fg(#73f0ff) $freemem  ^fg($iconcolour)^i($icons/sm4tik/net_up_02.xbm)^fg(#73f0ff) $tx ^fg($iconcolour)^i($icons/sm4tik/net_down_02.xbm)^fg(#73f0ff) $rx  ^fg(#cdcdcd)^i($icons/sm4tik/fs_01.xbm)^fg(#73f0ff) ${diskused}/${disktotal}B ^fg($batcolour)^i($baticon)^fg(#73f0ff) ${battery}% ^fg($iconcolour)^i($icons/sm4tik/clock.xbm)^fg(#73f0ff) $date "

#  sleep $interval

end 

##dzen2 -p -ta r -x 400 -y 0 -w 880 -h 12 -fn '-adobe-helvetica-medium-r-normal--11-*' -e 'onexit=ungrabmouse'

